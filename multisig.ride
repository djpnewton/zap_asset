# Define the public keys corresponding to the four private keys which
# will control the account.
let blackPubkey = base58''
let redPubkey = base58'nAB1nXdJb2L3CBupmPywXgsTy4AxwGriiPBjnQduvRt'
let greenPubkey = base58'HkLKJaSuCVZLrkEiKMoR3GYYPWg7D9vTZbsdZPZYfZUb'
let bluePubkey = base58'87xM2BfqBMtMp5PgDH5zfzvCCJWEH2zGMUUdbTVALwkg'

# Check which of the four private keys have signed the transaction.
let blackSigned = sigVerify(tx.bodyBytes, tx.proofs[0], blackPubkey)
let redSigned = sigVerify(tx.bodyBytes, tx.proofs[1], redPubkey)
let greenSigned = sigVerify(tx.bodyBytes, tx.proofs[2], greenPubkey)
let blueSigned = sigVerify(tx.bodyBytes, tx.proofs[3], bluePubkey)

# Compute the transaction's "signature score".
let blackScore = if(blackSigned) then 2 else 0
let redScore = if(redSigned) then 1 else 0
let greenScore = if(greenSigned) then 1 else 0
let blueScore = if(blueSigned) then 1 else 0
let sigScore = blackScore + redScore + greenScore + blueScore

# Authorise the transaction if and only if its signature score is at
# least 3.  This corresponds to the transaction being signed by any
# three keys, or being signed by the black key and any other key.
sigScore >= 3
